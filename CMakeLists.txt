# =====================================================================
#               DMOD RAM File System Module
# =====================================================================
cmake_minimum_required(VERSION 3.18)


# ======================================================================
#               DMOD FFS
# ======================================================================
# Allow version to be passed as a parameter, default to 0.1
if(NOT DEFINED DMOD_MODULE_VERSION)
    set(DMOD_MODULE_VERSION "0.1" CACHE STRING "DMOD module version")
endif()

# ======================================================================
#               Fetch DMOD repository
# ======================================================================
include(FetchContent)
FetchContent_Declare(
    dmod
    GIT_REPOSITORY https://github.com/choco-technologies/dmod.git
    GIT_TAG        develop
)

# ======================================================================
#               DMOD Configuration
# ======================================================================
set(DMOD_MODE "DMOD_MODULE" CACHE STRING "DMOD build mode")
set(DMOD_BUILD_TESTS OFF CACHE BOOL "Build tests")
set(DMOD_BUILD_EXAMPLES OFF CACHE BOOL "Build examples")
set(DMOD_BUILD_TOOLS OFF CACHE BOOL "Build tools")
set(DMOD_BUILD_TEMPLATES OFF CACHE BOOL "Build templates")
set(DMOD_ENABLE_MEMORY_ANALYSIS OFF CACHE BOOL "Enable memory analysis during build")

FetchContent_MakeAvailable(dmod)

project(dmramfs
    VERSION ${DMOD_MODULE_VERSION}
    DESCRIPTION "DMOD RAM File System"
    LANGUAGES C CXX)
set(DMOD_DIR ${dmod_SOURCE_DIR} CACHE PATH "DMOD source directory")
    
# =====================================================================
#               Fetch dmlist repository (headers only)
# =====================================================================
FetchContent_Declare(
    dmlist
    GIT_REPOSITORY      https://github.com/choco-technologies/dmlist.git
    GIT_TAG             master
)
# Only populate (download) dmlist, don't build it
FetchContent_Populate(dmlist)

# ======================================================================
#               Import dmod functions and macros
# ======================================================================
set(DMOD_DIR ${dmod_SOURCE_DIR} CACHE PATH "DMOD source directory")
include(${DMOD_DIR}/paths.cmake)
dmod_setup_external_module()

# ======================================================================
#               Fetch DMOD File System Interface
# ======================================================================
FetchContent_Declare(
    dmfsi
    GIT_REPOSITORY
    https://github.com/choco-technologies/dmfsi.git
    GIT_TAG        master
)
FetchContent_MakeAvailable(dmfsi)

# ======================================================================
#               DMFFS Module Configuration
# ======================================================================
# Name of the module 
set(DMOD_MODULE_NAME        dmramfs)

# Version is already set above and used in project()
# No need to set it again here

# Author (should be string)
set(DMOD_AUTHOR_NAME        Patryk Kubiak)

# Stack size for the module (should be integer)
set(DMOD_STACK_SIZE         1024)

#
#   dmod_add_library - create a library module
#   it has the same signature as add_library
#   and can be used in the same way after the creation
#   (for example, to link libraries)
#
dmod_add_library(${DMOD_MODULE_NAME} ${DMOD_MODULE_VERSION}
    # List of source files - can include C and C++ files
    src/dmramfs.c
)

target_include_directories(${DMOD_MODULE_NAME} PRIVATE
    ${CMAKE_CURRENT_SOURCE_DIR}/include
    ${dmlist_SOURCE_DIR}/include
)

# Link to DMFSI interface
target_link_libraries(${DMOD_MODULE_NAME} 
    dmfsi_if
    )

